apply plugin: 'java'
apply plugin: 'me.tatarka.retrolambda'

targetCompatibility = JavaVersion.VERSION_1_8
sourceCompatibility = JavaVersion.VERSION_1_8

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

dependencies {
    Map<String, String> dependencies = rootProject.ext.dependencies;

    compile project(':library')
    compile dependencies.rxjava
    compile dependencies.retrofitAdapterRxJava
    testCompile dependencies.junit
    testCompile dependencies.hamcrestAll
}

jar {
    manifest {
        attributes(
                'Main-Class': 'net.myacxy.retrotwitch.RetroTwitch',
        )
    }
}

task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = 'all'
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

artifacts {
    archives fatJar
}
